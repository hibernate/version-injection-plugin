buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'java-gradle-plugin'

    // publishing
    id "com.gradle.plugin-publish" version "1.2.1"
    id "nu.studer.credentials" version "2.1"
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = 'org.hibernate.build.gradle'
version = '2.0.0-SNAPSHOT'

buildDir = "target"

dependencies {
    compileOnly gradleApi()
    implementation 'org.javassist:javassist:3.18.1-GA'
}

java {
    withSourcesJar()
    withJavadocJar()
}

gradlePlugin {
    website = "https://github.com/hibernate/version-injection-plugin"
    vcsUrl = "https://github.com/hibernate/version-injection-plugin"

    plugins {
        injection {
            id = 'org.hibernate.build.version-injection'
            implementationClass = "org.hibernate.build.gradle.inject.InjectionPlugin"
            displayName = "Version Injection Plugin"
            description = "Gradle plugin for performing injection of the project's version into the bytecode of one or more members"
        }
    }
}

ext {
    //noinspection GrUnresolvedAccess
    project.setProperty( 'gradle.publish.key', credentials.hibernatePluginPortalUsername )
    //noinspection GrUnresolvedAccess
    project.setProperty( 'gradle.publish.secret', credentials.hibernatePluginPortalPassword )
}
